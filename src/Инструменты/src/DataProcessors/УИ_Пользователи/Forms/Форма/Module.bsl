
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УИ_ОбщегоНазначения.ФормаИнструментаПриСозданииНаСервере(ЭтотОбъект,
															 Отказ,
															 СтандартнаяОбработка,
															 КоманднаяПанель);
															 
	СуществуетПодсисетмаПользователи = УИ_ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Пользователи");
					
	ЗаполнитьПользователей();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗапуститьСеансПользователя(Команда)

	ТекущиеДанные = Элементы.Пользователи.ТекущиеДанные;

	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуЗапускаСеансаПользователя(ТекущиеДанные)
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьСеансПользователяСДопПараметрами(Команда)
	
	ТекущиеДанные = Элементы.Пользователи.ТекущиеДанные;

	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуЗапускаСеансаПользователя(ТекущиеДанные, Ложь)
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьОбщуюКомандуИнструментов(Команда) 
	УИ_ОбщегоНазначенияКлиент.Подключаемый_ВыполнитьОбщуюКомандуИнструментов(ЭтотОбъект, Команда);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ОткрытьФормуЗапускаСеансаПользователя(ДанныеСтроки, АвтоЗапуск = Истина)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ИмяПользователяИБ", ДанныеСтроки.ПользовательИБ);
	ПараметрыОткрытияФормы.Вставить("УИДПользователяИБ", ДанныеСтроки.УИДПользователяИБ);
	ПараметрыОткрытияФормы.Вставить("АвтоЗапуск", АвтоЗапуск);

	ОткрытьФорму("Обработка.УИ_Пользователи.Форма.ФормаЗапускаСеанса", ПараметрыОткрытияФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПользователей()
	
	Пользователи.Очистить();
	
	ПользователиИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();
	
	Для Каждого СтрПользователиИБ Из ПользователиИБ Цикл
	
		нсПользователи = Пользователи.Добавить();
		нсПользователи.ПользовательИБ = СтрПользователиИБ.Имя;
		нсПользователи.УИДПользователяИБ = СтрПользователиИБ.УникальныйИдентификатор;
		
	КонецЦикла;
	
	
КонецПроцедуры

#КонецОбласти
